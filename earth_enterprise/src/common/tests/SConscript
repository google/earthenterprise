#-*- Python -*-
#
# Copyright 2017 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

Import('env')
import os

env.Append(LIBPATH = ['/usr/lib/', '/usr/lib64'])

map(lambda test: env.test(test, [test + '.cpp'],
                          LIBS=['gecommon', 'geutil', 'png12']),
    ['simpleexception_unittest',
     'endian_tests',
     'segmented_array_tests',
     'geFileUtils_test',
     'filepool_tests',
     'lockfileguard_tests',
     'projection_test',
     ])


map(lambda test: env.test(test, [test + '.cpp'],
                          LIBS=['geutil', 'png12']),
    ['multirange_tests',
     'dottedversion_tests',
     ])


map(lambda test: env.test(test, [test + '.cpp'],
                          LIBS=['gecommon', 'geutil', 'gtest', 'png12']),
    ['khStringUtils_test',
     'khfileutils_tests',
     'khMisc_tests',
     ])

map(lambda test: env.test(test, [test + '.cc'],
                          LIBS=['gecommon', 'geutil', 'gtest', 'png12']),
    ['etencoder_tests',
     ])

# Compile this test with address sanitizer to detect buffer overflows
asan = env.Copy()
asan_flags = ['-fsanitize=address', '-fno-omit-frame-pointer', '-fno-common']
asan.Append(test_extra_cppflags = asan_flags)
asan.Append(test_linkflags = asan_flags)
asan.test('etencoder_buffer_tests', ['etencoder_buffer_tests.cpp'])

testdir = os.path.join(env.exportdirs['bin'],'tests')
env.copyfile(testdir, 'RunAllTests.pl')

# Drop in the test data
sync_test_data = env.Command(
    '%s/tests/fusion' % Dir(env.exportdirs['bin']).abspath, # TARGET
    Dir('#fusion/testdata').abspath, # SOURCE
    env.rsync_cmd % (
        '$SOURCE',
        '$TARGET'))
# Always sync the test data. rsync will make sure it doesn't do unnecessary work.
env.AlwaysBuild(sync_test_data)

env.sharedLib('getests', ['qtpathgen.cpp'])
