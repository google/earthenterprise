#
# Copyright 2017 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

# -*- Python -*-
Import('env')

build_root = Dir('.').abspath


libgepublish = env.sharedLib('gepublish',
                             ['CurlRequest.cpp', 'PublisherClient.cpp',
                              'PublisherConstants.cpp', 'PublishHelper.cpp'],
                             LIBS=['curl', 'geutil', 'dbmanifest',
                                   'gecommon'])

env.test('curl_request_unittest',
         'CurlRequest_unittest.cpp',
         LIBS=['gepublish', 'curl', 'gtest'])

geserveradmin = env.executable('geserveradmin', 'geserveradmin.cpp',
                           LIBS=['dbmanifest', 'gepublish', 'gemiscconfig',
                                 'geutil', 'gecommon', 'geautoingest', 'QtCore', 'QtGui',
                                 'geconfigutil', 'xerces-c'])

swig_env = env.DeepCopy()
swig_env['STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME']=1
root_dir = Dir(swig_env.exportdirs['root']).abspath

cxx_flags = swig_env['CXXFLAGS']
cxx_flags.remove('-Werror')
swig_env['CXXFLAGS'] = cxx_flags

swig_env['CPPFLAGS'] += ['-fPIC']  # Needed for 64-bit compilation.
swig_env['CPPFLAGS'] += ['-I%s' % swig_env['python_include']]
swig_env.Append(SWIGFLAGS = ['-python', '-c++'])

libgepublishmanagerhelper = swig_env.sharedLib('gepublishmanagerhelper',
                                               ['gepublishmanagerhelper.cpp', 'gepublishmanagerhelper.i'],
                                               LIBS=['geutil', 'dbmanifest', 'gecommon'])


env.install('common_lib', [libgepublish])
# Note: putting in common_{lib, bin}, we provide it for both Fusion and Server
# installer by default.
#env.install('disconnected_lib', [libgepublish])
env.install('common_bin', [geserveradmin])
#env.install('disconnected_bin', [geserveradmin])

# Install libgepublishmanagerhelper.so, libgepublishmanagerhelper.py into 'gehttpd/wsgi-bin/serve/publish'
env.installAs('httpdwsgi', [libgepublishmanagerhelper], '_libgepublishmanagerhelper.so', 'serve/publish')
env.InstallFileOrDir(
    '%s/libgepublishmanagerhelper.py' % build_root,
    '%s/serve/publish' % env.installdirs['httpdwsgi'],
    libgepublishmanagerhelper, 'install')

